apply plugin: 'base'

buildscript {

    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'bintray'

    group = 'org.open-dolphin'
    version = '0.1-SNAPSHOT'
    project.archivesBaseName = 'od-example-pms-server'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'http://dl.bintray.com/svene/mavenrepo'
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['mavenJava']
        pkg {
            userOrg = 'svene'
            repo = 'mavenrepo'
            name = 'open-dolphin-example-pms'
            licenses = ['Apache-2.0']
        }
    }

    task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
        logger.info "Installing $project.name"
    }


}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.akhikhl.gretty:gretty:1.1.8"
  }
}

apply plugin : 'war'
apply plugin: 'org.akhikhl.gretty'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'http://dl.bintray.com/opendolphin/mavenrepo' }

	ivy {
		name 'OpenDolphinJS'
		url 'http://dl.bintray.com/opendolphin/resources/'
		layout 'pattern', {
			artifact '[module]-[revision](.[classifier]).[ext]'
		}
	}
}

configurations {
	opendolphinjs
}

dependencies {
    compile project(':server')
    compile project(':shared')
    compile 'org.webjars:bootstrap:3.3.1'
    compile 'org.webjars:angularjs:1.2.21'
    compile 'org.open-dolphin:open-dolphin-ng:0.12.0B1'
    opendolphinjs group: 'opendolphin', name: 'opendolphin', version: "$dolphinJS_version", ext: 'js'
}

task fetchWebappArtifacts(type: Copy) {
	from configurations.opendolphinjs
	into "$webAppDir/generated/js/"
}

war {
    archiveName='application.war'
}

gretty {
  contextPath = '/appContext'
  //port = 8081
  scanInterval = 2
}

project.afterEvaluate {
	// Let gretty-tasks depend on 'fetchWebappArtifacts'. Note: this cannot be done with:
	//  tasks.findAll { task -> ['appRun', 'appRunWar', 'war'].contains(task.name) }*.dependsOn fetchWebappArtifacts
	// do to how the gretty plugin is implemented. Therefore it is done in this 'project.afterEvaluate':

	[appRun, appRunWar, war]*.dependsOn fetchWebappArtifacts
}
